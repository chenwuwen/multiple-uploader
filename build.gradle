plugins {
    id 'java'
}

allprojects {

    group 'cn.kanyun'
    version '1.0.0'
    sourceCompatibility = 1.8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}

subprojects {

    apply plugin: 'java'
//    需要注意的而是,gradlew clean install时并没有把依赖打入到jar包中,而是生成了一个与jar包名称一致的pom文件,里面记录了依赖的信息,当上传jar包时,该pom文件也会被上传
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    //    签名插件,上传到中央仓库的release版本的依赖需要签名
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.java
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }


    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
        options.author = true
        options.version = true
        options.header = project.name

    }

    /**
     * 默认使用gradlew clean install命令时,仅仅只是安装编译好的包,源码包
     * 是不会安装到maven仓库的,添加此配置可以将源码包打入到本地maven仓库
     * doc包也可以如法炮制
     */
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

//   设置任务依赖关系,打jar包的同时,打源码包
    jar.dependsOn(sourcesJar)



    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'

        implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'

        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

//    声明注解处理器的注解,用于代替手动编辑resources/META-INF/services下的文件
        implementation group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc6'
//    这行配置也需要添加,gradle升级到5之后,不加此配置,不会生成META-INF/services/下的文件
        annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc6'

        compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.29'
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compileOnly group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    }
}


project(':multiple-uploader-qiniu') {
    description = '七牛云桥接器'
    dependencies {
        compileOnly project(':multiple-uploader-api')
    }
}


project(':multiple-uploader-upyun') {
    description = '又拍云桥接器'
    dependencies {
        compileOnly project(':multiple-uploader-api')
    }
}



